********************************************************************************************************
*  NewLoanCash.txt 
*
*  This script reads Plan Position Accounts for the last 7 calendar days, finding POOLLOAN3 position records
*  and the Secondary1Buys loaded from TRUSTTRANS.P1 and builds the necessary C1 activity for the right side (AC)
*  of the cash reconciliation.  Each record it finds, it checks UDF1 in POPP877 to see if it's value matches 
*  the expected amount, if so, that record has already been updated.
*
*  12/21/2023 - Gary Matten - created OmniScript
*  06/27/2024 - Gary Matten - GPD-1704 - re-correcting position 92 to be a value of 2 instead of 1
*  09/25/2024 - Gary Matten - Recognize Loan Reversal Activity and Net Activity Correctly when there are BUYS and SELLS
********************************************************************************************************
sd080 = 99999999;
OcLVar_Define(x.FileName n.SevenDaysAgo x.RKPlan n.TradeDate n.NewLoanUnits n.PriorCashApplied x.Line x.TrustAccount n.LastBusiness 
              n.Secondary1Buys n.Secondary1Sells n.RunDate);
FileName = OcText_string(OCTEXT_GETENV('$XDAT') '\OTDALY.OMNISCRIPT.C1.NEWLOANOFFSET.' 
           OCFMT(OcDate_Current() 'Z8') '.' OcFMT(OcTime_Current() 'Z6') '.DAT');
OcShow(FileName);
OcFile1_Open(name:FileName mode:'OUTPUT');

RunDate      = octext_tonum(octext_getenv('$RUN-DATE'));
if OcDate_Valid(RunDate);
   SevenDaysAgo = OcDate_AddDays(RunDate -7);
   LastBusiness = OcDate_AddBusDays(RunDate -1);
else;
   SevenDaysAgo = OcDate_AddDays(OcDate_Current() -7);
   LastBusiness = OcDate_AddBusDays(OcDate_Current() -1);
end;
OcShow(RunDate SevenDaysAgo LastBusiness);

poppobj_view(securityid:'POOLLOAN3' datelo:SevenDaysAgo datehi:LastBusiness);
loop while poppobj_next();
  RKPlan           = poppobj_de(030);
  TradeDate        = poppobj_numde(008);
  Secondary1Buys   = poppobj_numde(741);
  PriorCashApplied = poppobj_numde(877);
  If (Secondary1Buys <> 0);                  /* CHECK SSSA FOR POSSIBLE REVERSAL */
     PERFORM 'CHECK.SSSA';
  End;
  if (PriorCashApplied <> Secondary1Buys) and (Secondary1Buys <> 0);
         RKPlan           = poppobj_de(030);
         TradeDate        = poppobj_numde(008);
         NewLoanUnits     = 0 - Secondary1Buys;
         TrustAccount     = poppobj_de(01510);
         OcText_Set(Line 1 'C100' 4);
         OcText_Set(Line 5 RKPlan 6);
         OcText_Set(Line 31 OcFmt(LastBusiness 'Z8') 8);
         OcText_Set(Line 40 TrustAccount 32);
         OcText_Set(Line 73 '000000000000000    2' 20);
         OcText_Set(Line 115 '0' 1);
         OcText_Set(Line 116 OcFmt(NewLoanUnits 'Z,12V2-') 15);
         OcText_Set(Line 134 '00339' 5);
         OcFile1_Write(Line);
         poppobj_setde(denum:877 value:Secondary1Buys);
         poppobj_update();         
  end;   
endloop;

ROUTINE 'CHECK.SSSA';
if (RKPlan <> '') and (TradeDate <> 0);
   WK001 = 0;
   sssaobj_view(PLAN:RKPlan SECURITYID:'POOLLOAN3' DATE:TradeDate);
   loop while sssaobj_next();
      if sssaobj_de(011) = 'XI';
         if sssaobj_de(009) = 'B';
            WK001 = WK001 + sssaobj_numde(235);
         end;
         if sssaobj_de(009) = 'S';     /* REVERSAL ACTIVITY */
            WK001 = WK001 - sssaobj_numde(235);
         end; 
      end;
   endloop;
   Secondary1Buys = WK001;
end;
GOBACK;



